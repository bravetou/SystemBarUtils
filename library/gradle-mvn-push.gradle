// 配置插件
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.1" // 这个是版本号，必须填写
def siteUrl = 'https://github.com/bravetou/SystemBarUtils' // 项目的主页   这个是说明，可随便填
def gitUrl = 'https://github.com/bravetou/SystemBarUtils' // Git仓库的url  这个是说明，可随便填
def issueUrl = 'https://github.com/bravetou/SystemBarUtils/issues'
//compile 'com.arshowbaby:mylibrary:1.0.0'中的com.arshowbaby
group = "com.brave.system.bar" // 这里是groupId ,必须填写  一般填你唯一的包名

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // 项目描述
                name 'Android SystemBar Utils'
                // 项目主页
                url siteUrl
                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 填写开发者基本信息
                developers {
                    developer {
                        id 'bravetou'
                        name 'brave tou'
                        email 'bravetou@163.com'
                    }
                }
                // SCM
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去
// 所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    // 读取 local.properties 文件里面的 bintray.user
    user = properties.getProperty("bintray.user")
    // 读取 local.properties 文件里面的 bintray.apikey
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "braveorg" // 发布到JCenter的组织
        repo = "maven" // 发布到JCenter上的仓库名称
        name = "SystemBarUtils" // 发布到Bintray上的项目名字
        desc = 'Android SystemBar Utils'
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        labels = ['android']
        publish = true // 是否是公开项目
        publicDownloadNumbers = true
    }
}